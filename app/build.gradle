apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'jacoco-android'
apply from: "$project.rootDir/keystore/secret.gradle"
apply from: "$project.rootDir/tools/version.gradle"

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "ru.andrikeev.android.synoptic"
        minSdkVersion 15
        targetSdkVersion 26
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        buildConfigField 'String', 'API_KEY', "\"${apiKeyWeather}\""
        buildConfigField 'String', 'API_KEY_PLACES', "\"${apiKeyPlaces}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

    signingConfigs {
        debug {
            keyAlias debugAlias
            keyPassword debugAliasPass
            storePassword debugKeystorePass
            storeFile file(debugKeystore)
        }
        release {
            keyAlias alias
            keyPassword aliasPass
            storePassword keystorePass
            storeFile file(keystore)
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            testCoverageEnabled = true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), "../tools/proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        dev {
            buildConfigField 'String', 'BASE_URL_WEATHER', '"http://api.openweathermap.org/data/2.5/"'
            buildConfigField 'String', 'BASE_URL_PLACES', '"https://maps.googleapis.com/maps/api/place/"'
            signingConfig signingConfigs.debug
            versionCode gitVersionCodeTime
            versionName gitVersionName
            applicationIdSuffix '.dev'
        }
        prod {
            buildConfigField 'String', 'BASE_URL_WEATHER', '"http://api.openweathermap.org/data/2.5/"'
            buildConfigField 'String', 'BASE_URL_PLACES', '"https://maps.googleapis.com/maps/api/place/"'
            signingConfig signingConfigs.release
            versionCode gitVersionCode
            versionName gitVersionName
        }
    }

    sourceSets {
        test {
            resources.srcDirs += ['src/test/res']
        }
    }

    jacocoAndroidUnitTestReport {
        excludes += ['**/*AutoValue*.*', 
        '**/**Type.*', 
        '**/Dagger*.*',
        '**/AppInjector*.*',
        '**/*Module.*',
        '**/*_Factory.*',
        '**/MoxyReflector*.*'
        ]

        csv.enabled false
        html.enabled true
        xml.enabled true
    }

}

ext {
    support_version = '26.0.+'
    constraint_version = '1.0.2'
    dagger_version = '2.11'
    jsr250_version = '1.0'
    licensesdialog_version = '1.8.1'
    retrofit_version = '2.2.0'
    rx_java_version = '2.0.7'
    rx_android_version = '2.0.1'
    rx_binding_version = '2.0.0'
    moxy_version = '1.5.3'
    gson_version = '2.8.0'
    requery_version = '1.3.0'
    canary_version = '1.5.1'
    timber_version = '4.5.1'
    jobs_version = '1.1.11'
    stetho_version = '1.5.0'
    espresso_version = '2.2.2'
    junit_version = '4.12'
    junitparams_version = '1.1.0'
    hamcrest_version = '1.3'
    support_test_runner = '0.5'
    mockito_version = '2.+'
    autovalue_version = '1.2'
    autovalue_gson_version = '0.5.0'
    dotsindicator_version = '1.0.1'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // tests
    androidTestCompile "com.android.support.test.espresso:espresso-core:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestCompile "com.android.support:support-annotations:$support_version"
    androidTestCompile "com.android.support.test:runner:$support_test_runner"
    androidTestCompile "com.android.support.test:rules:$support_test_runner"
    androidTestCompile "org.hamcrest:hamcrest-library:$hamcrest_version"

    testCompile "junit:junit:$junit_version"
    testCompile "pl.pragmatists:JUnitParams:$junitparams_version"
    testCompile "org.mockito:mockito-core:$mockito_version"

    // support
    compile "com.android.support:support-annotations:$support_version"
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:preference-v7:$support_version"
    compile "com.android.support:preference-v14:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:support-vector-drawable:$support_version"
    compile "com.android.support.constraint:constraint-layout:$constraint_version"
    // dagger2
    compile "com.google.dagger:dagger:$dagger_version"
    compile "com.google.dagger:dagger-android:$dagger_version"
    compile "com.google.dagger:dagger-android-support:$dagger_version"
    apt "com.google.dagger:dagger-compiler:$dagger_version"
    apt "com.google.dagger:dagger-android-processor:$dagger_version"
    // moxy
    compile "com.arello-mobile:moxy:$moxy_version"
    compile "com.arello-mobile:moxy-app-compat:$moxy_version"
    apt "com.arello-mobile:moxy-compiler:$moxy_version"
    // retrofit2
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    // requery
    compile "io.requery:requery:$requery_version"
    compile "io.requery:requery-android:$requery_version"
    apt "io.requery:requery-processor:$requery_version"
    // gson
    compile "com.google.code.gson:gson:$gson_version"
    // rx
    compile "io.reactivex.rxjava2:rxjava:$rx_java_version"
    compile "io.reactivex.rxjava2:rxandroid:$rx_android_version"
    compile "com.jakewharton.rxbinding2:rxbinding:$rx_binding_version"
    // jsr
    provided "javax.annotation:jsr250-api:$jsr250_version"
    // licenses
    compile "de.psdev.licensesdialog:licensesdialog:$licensesdialog_version"
    // canary
    debugCompile "com.squareup.leakcanary:leakcanary-android:$canary_version"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$canary_version"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$canary_version"
    // timber
    compile "com.jakewharton.timber:timber:$timber_version"
    // jobs
    compile "com.evernote:android-job:$jobs_version"
    // stetho
    compile "com.facebook.stetho:stetho:$stetho_version"
    // autovalue
    provided "com.google.auto.value:auto-value:$autovalue_version"
    apt         "com.google.auto.value:auto-value:$autovalue_version"
    provided "com.ryanharter.auto.value:auto-value-gson:$autovalue_gson_version"
    apt "com.ryanharter.auto.value:auto-value-gson:$autovalue_gson_version"

    compile "com.tbuonomo.andrui:viewpagerdotsindicator:$dotsindicator_version"
}
